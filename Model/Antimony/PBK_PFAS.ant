// Based on Westerhout et al., 2024 PFOA PBPK model

model PFOA()

  // PHYSIOLOGY
  BW = 70             # Body weight (perhaps make time dependent: input age and bw-at-age)

  // Perfusion fractions
  QCC = 300;          # 12.5*24   # Cardiac blood output (L/d/kg^0.75)
  QFC = 0.052;        # Fraction cardiac output going to fat
  QLC = 0.069;        # Fraction cardiac output going to liver suggested by EFSA is 0.069, since they corrected for output to the gut Luccisano used 0.25, but Brown actually report 0.23
  QKC = 0.175;        # Fraction cardiac output going to kidney
  QSkC = 0.058;       # Fraction cardiac output going to skin
  QGC = 0.181;        # Fraction of cardiac output going to gut and the liver via portal arthery

  // Fraction tissue volumes
  VLC = 0.026;        # Fraction liver volume
  VFC = 0.214;        # Fraction fat volume
  VKC = 0.004;        # Fraction kidney volume
  VfilC = 0.0004;     # Fraction filtrate compartment volume (10% of kidney volume)
  VGC = 0.0171;       # Fraction gut volume
  VlunC = 0.007       # Fraction lung volume (ICRP 89)
  VPlasC = 0.0428;    # Fraction plasma volume (58% of blood)
  Htc = 0.44;         # hematocrit

  // Skin parameters 
  Skinarea = 972      # Exposed area on skin (cm^2) 
  Skinthickness = 0.1 # Skin thickness (cm) 
  fss = 0.005         # fraction palm of hands (Sheridan et al., 1995, Rhodes et al., 2013)
  hsurf = 0.01        # applied layer thickness in cm, value is assumed to be 0.1 mm
  hsc = 0.0015        # Stratum corneum thickness (cm) # [Kr端se 2007]
  hve = 0.0100        # Viable epidermis thickness (cm) # [Kr端se 2007]
  hcell = 0.00005     # Blood vessel wall thickness (cm) # [Burton 1954] = 0.5 um capillary wall thickness
  ffatsc = 0.05       # Fraction of fat in stratum corneum [Polak 2012]
  ffatve = 0.02       # Fraction of fat in viable epidermis [Polak 2012]
  ffatepi = 0.02      # Fraction of fat in epidermis [Polak 2012]
  ffatbl = 0.007      # Fraction of fat in blood [Polak 2012]
  Kpcell = 0.93       # cm/h; Kr端se model is normalized to 1 cm^2 # Permeability coefficient from arterial wall into the blood compartment [Kr端se 2007]

  // Physicochemical properties
  MW = 414.07
  logP = 4.81
  VP = 2.34           # Vapor pressure (Pa) 10^0.37 [Zhang 2021]; 1 mmHg = 133.322368 Pa; 0.15 mmHg (https://haz-map.com/Agents/6596) = 20 Pa

  Tmc = 144000        # 6000*24  # Maximum resorption rate (ug/d/kg^.75)^a (based on half-life of 2.3 years in humans)
  Kt = 55             # Resorption affinity (ug/L) (same as monkey)
  kurinec = 0.0072    # 0.0003*24 # urinary elimination rate constant (/d/kg^-0.25)# estimated 
  Free = 0.02         # Free fraction in plasma# same as monkey 

  // Inhalation exposure
  Biotransformation = 0.003 # 8:2-FTOH to PFOA [Gomis 2016]

  // Partition coefficients from Harada, et al 2005 
  PL = 2.2            # Liver/plasma partition coefficient
  PF = 0.04           # Fat/plasma partition coefficient
  PK = 1.05           # Kidney/plasma partition coefficient
  PSk = 0.1           # Skin/plasma partition coefficient
  PR = 0.12           # Rest of the body/plasma partition coefficient
  PG = 0.05           # Gut/blood plasma coefficient
  PLun = 1.27         # Lung/plasma partition coefficient; [Fabrega 2014]

  // Dermal exposure
  Kpsc = 0.000088     # [Franko 2011]
  Kpve = 0.000088     # [Franko 2011]

  // Assignments
  kurine := kurinec*BW^(-0.25)
  
  // Dermal exposure
  SkinTarea := 9.1*((BW*1000)^0.666)  # Total area of the skin (cm^2)
  Kscve := ((1-ffatve)+(ffatve*(10^logP)))/((1-ffatsc)+(ffatsc*(10^logP)))
  Kver := ((1-ffatbl)+(ffatbl*(10^logP)))/((1-ffatepi)+(ffatepi*(10^logP)))
  Pab := 1/(10^(6.96-(1.04*log10(VP)) - 0.533*logP - 0.00495*MW))

  // Free fraction of chemical in tissues 
  FreeL := Free/PL;           # liver
  FreeF := Free/PF;           # fat
  FreeK := Free/PK;           # kidney
  FreeSk := Free/PSk;         # skin
  FreeR := Free/PR;           # rest of the body
  FreeG := Free/PG;           # gut
  FreeLun := Free/PLun;       # lung
  
  // Scaled cardiac output and perfusion
  QC := QCC*BW^0.75;          # Cardiac output adjusted for BW (L/d)
  QCP := QC*(1-Htc) ;         # Cardiac output adjusted for plasma flow (L/d)
  QL := QLC*QCP;              # Scaled plasma flow to liver (L/d)
  QF := QFC*QCP;              # Scaled plasma flow to fat (L/d)
  QK := QKC*QCP;              # Scaled plasma flow to kidney (L/d)
  Qfil := 0.2*QK              # Plasma flow to filtrate compartment (L/d)# 20% of QK 
  QG := QGC*QCP;              # Scaled plasma flow to gut (L/d)
  QSk := QSkC*QCP             #*(Skinarea/SkinTarea); # scaled plasma flow to the skin
  QR := QCP-QL-QF-QK-QG-QSk; # Plasma flow to the rest of the body.
  Qbal := QCP - (QR+QL+QF+QK+QG+QSk)
 
  // Scaled tissue volumes
  VL := VLC*BW;               # Scaled liver volume (L)
  VF := VFC*BW;               # Scaled fat volume (L)
  VK := VKC*BW;               # Scaled kidney volume (L)
  Vfil := VfilC*BW;           # Scaled filtrate compartment volume
  VG := VGC*BW;               # Gut volume (L)
  VPlas := VPlasC*BW;         # Scaled plasma volume(L)
  Vart_plas := 0.39*VPlas     # Scaled arterial plasma volume [Brown 1997]
  Vven_plas := 0.61*VPlas     # Scaled venous plasma volume [Brown 1997]
  VSk := (Skinarea*Skinthickness)/1000;  # Skin volume (L)
  Vlun := VlunC*BW # Functional residual capacity # https://en.wikipedia.org/wiki
  VR := 0.84*BW-VL-VF-VK-Vfil-VG-VPlas-VSk-Vlun;  # Rest of the body volume (L). Need to know where the number 0.84 comes from???  
  VT := 0.007*BW              # Tidal volume # https://en.wikipedia.org/wiki/Tidal_volume
  // Functional_residual_capacity
  VFRC := 0.030*BW            # Tidal volume
  Valv := VFRC + 0.5*VT       # Alveolar volume
  Vbal := (0.84*BW)-(VL+VF+VK+Vfil+VG+VPlas+VSk+Vlun) # Balance check--better be 0 
  Vsurf := fss*SkinTarea*hsurf/1000       # volume of dermal layer on exposed skin surface (L) 1 m^2 = 10,000 cm^2 * cm = cm^3 = ml / 1000 = L
  Vsc := fss*SkinTarea*hsc/1000           # volume of exposed stratum corneum (L) cm^2 * cm = cm^3 = ml / 1000 = L
  Vve := fss*SkinTarea*hve/1000           # volume of exposed viable epidermis (L) cm^2 * cm = cm^3 = ml / 1000 = L  
  
  Tm := Tmc*BW^0.75 # transporter maximum 

  CLsc := 24*Kpsc*fss*SkinTarea/1000      # stratum corneum clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  CLve := 24*Kpve*fss*SkinTarea/1000      # viable epidermis clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  CLcell := 24*Kpcell*fss*SkinTarea/1000  # clearance from arterial wall into the blood compartment (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  
  // Define compartments
  compartment Lung := Vlun      # Lung
  compartment Trans = 0.0000001 # Transfer compartment
  compartment Sc                # Stratum corneum
  compartment Ve := Vve         # Viable epidermis
  compartment Skin := VSk;      # Skin compartment
  compartment Ven := Vven_plas
  compartment Art := Vart_plas 
  compartment Gut := VG;        # Gut compartment
  compartment Liv := VL;        # Liver compartment
  compartment Fat := VF;        # Fat compartment
  compartment Kid := VK;        # Kidney compartment
  compartment Fil := Vfil       # kidney filtrate compartment
  compartment Delay;            # Storage compartment of urine
  compartment Urine;            # Urine compartment
  compartment Rest := VR        # Rest compartment
  
  substanceOnly species Alung in Lung
  substanceOnly species Atrans in Trans
  substanceOnly species Asc in Sc
  substanceOnly species Ave in Ve
  substanceOnly species ASk in Skin
  substanceOnly species Aven in Ven
  substanceOnly species Aart in Art
  substanceOnly species AG in Gut
  substanceOnly species AL in Liv
  substanceOnly species AF in Fat
  substanceOnly species AK in Kid
  substanceOnly species Afil in Fil
  substanceOnly species Adelay in Delay
  substanceOnly species Aurine in Urine
  substanceOnly species AR in Rest
  
  Alung = 0
  Atrans = 0
  Asc = 0
  Ave = 0
  ASk = 0
  Aven = 0
  Aart = 0
  AG = 0        # amount in gut
  AL = 0;       # amount in liver
  AF = 0;       # amount in fat
  AK = 0;       # amount in kidney
  Afil = 0;     # amount in kidney filtrate
  Adelay = 0;   # amount in storage compartment of urine
  Aurine = 0;   # amount in urine
  AR = 0;       # amount in the rest of the body
  
  // Transfer equations
  
  Asc -> Atrans; 2*CLsc*(Asc / Vsc)
  Atrans -> Ave; 2*CLve*((Atrans / 0.0000001)/Kscve)
  Atrans -> Asc; 2*CLsc*(Atrans / 0.0000001)
  Ave -> Atrans; 2*CLve*(Ave / Vve)
  Ave -> ASk; CLcell*(Ave / Vve)/Kver
  
  Alung -> Aart; QCP*(Alung / (Valv*Pab + Vlun))
  Aart -> AG; QG*(Aart/Vart_plas)
  Aart -> AL; QL*(Aart/Vart_plas)
  Aart -> AF; QF*(Aart/Vart_plas)
  Aart -> AK; QK*(Aart/Vart_plas)
  Aart -> Afil; Qfil*(Aart/Vart_plas)
  Aart -> AR; QR*(Aart/Vart_plas)
  Aart -> ASk; QSk*(Aart/Vart_plas)
  
  AG -> AL; QG*FreeG*(AG/VG)
  Afil -> AK; (Tm*(Afil/Vfil))/(Kt+(Afil/Vfil))
  Afil -> Adelay; Qfil*(Afil/Vfil)
  Adelay -> Aurine; kurine*Adelay 
  
  AL -> Aven; (QL+QG)*FreeL*(AL/VL)
  AF -> Aven; QF*FreeF*(AF/VF)
  AK -> Aven; QK*FreeK*(AK/VK)
  AR -> Aven; QR*FreeR*(AR/VR)
  ASk -> Aven; QSk*FreeSk*(ASk/VSk)
  
  Aven -> Alung; QCP*(Aven/Vven_plas)
  
end
