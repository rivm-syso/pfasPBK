function conc(q,vol)
  piecewise(q/vol, vol > 1e-5, 0)
end

// Westerhout et al., 2024 PFOS PBPK model


model PFOS()
  
  // TIMING
  age_start = 20 # occupational exposure starting age
  age_stop = 60 # occupational exposure stopping age
  
  day := time + 1
  dayoftheweek := (time + 1) % 7
  month := ceiling(day/(365/12))-(((ceiling(day/365))-1)*12)
  year := ceiling(day/(365))
  age := time / 365 # used as a starting point, rather than birth [years]
  
  // EXPOSURE PARAMETERS
  exposure_duration = 18750 # Duration of exposure (d)
  age_start_derm_exp = 14 # dermal and inhalation exposure starting age (e.g. occupational, or personal care products; exposure may start at a later age than 0)
  age_stop_derm_exp = 50 # dermal and inhalation exposure stopping age (e.g. occupational, or personal care products; exposure may stop at an earlier age than 80)
  age_start_inh_exp = 0 # dermal and inhalation exposure starting age (e.g. occupational, or personal care products; exposure may start at a later age than 0)
  age_stop_inh_exp = 50 # dermal and inhalation exposure stopping age (e.g. occupational, or personal care products; exposure may stop at an earlier age than 80)

  // Drinking water exposure 
  Drinkrate = 13 # Drinking water rate (mL/kg/day)
  
  // Dermal exposure 
  Dermexpo = 0        # 0 = NO, 1 = YES
  Dermconc = 0.0      # Dermal concentration (mg/mL) 
  Dermvol = 0.001     # Dermal exposure volume (mL)
  Dermdose := Dermconc*Dermvol*1000 # (ug) 
  Skinarea = 972      # Exposed area on skin (cm^2) 
  Skinthickness = 0.1 # Skin thickness (cm) 
  
  // Oral exposure via the mother
  Oralexpo_PFOS = 0.000444 # µg/kg/day TWI of 4.4 ng/kg/wk assuming PFOS:PFOS ratio of 24:76 from [EFSA opinion 2020, p.152]
  Drinkconc_PFOS = 0 # Drinking water concentration (ug/L or ppb) 
  PFOSmaternal = 4.89 # maternal concentration ng/mL at delivery [EFSA opinion 2020, p.152]
  
  // Inhalation exposure
  Cinh_PFOS = 0 # ug/L; 0.526 ug/m3 [Nilsson 2013]
  Cinh_PFOS_total_day := Cinh_PFOS/4  # 6h exposure spread across a 24h day
  
  // Dermal exposure
  Cdermal_PFOS = 0 # ug/kg = 0 ug/g [Freberg 2010]
  Cdermal_PFOS_total_day := Cdermal_PFOS/4 # 6h exposure spread across a 24h day
  
  // PHYSIOLOGY
  
  // Perfusion fractions
  QCC = 300;     # 12.5*24   # Cardiac blood output (L/d/kg^0.75)
  QFC = 0.052;   # Fraction cardiac output going to fat
  QLC = 0.069;   # Fraction cardiac output going to liver suggested by EFSA is 0.069, since they corrected for output to the gut Luccisano used 0.25, but Brown actually report 0.23
  QKC = 0.175;   # Fraction cardiac output going to kidney
  QSkC = 0.058;  # Fraction cardiac output going to skin
  QGC = 0.181;   # Fraction of cardiac output going to gut and the liver via portal arthery
  
  // Fraction tissue volumes
  VLC = 0.026;   # Fraction liver volume
  VFC = 0.214;   # Fraction fat volume
  VKC = 0.004;   # Fraction kidney volume
  VfilC = 0.0004;# Fraction filtrate compartment volume (10% of kidney volume)
  VGC = 0.0171;  # Fraction gut volume
  VlunC = 0.007 # Fraction lung volume (ICRP 89)
  VPlasC = 0.0428;  # Fraction plasma volume (58% of blood)
  Htc = 0.44;  # hematocrit
  
  // Mother-child parameters
  BWbirth = 3.68 # Body weight at birth in kg new add opinion 2020 
  
  // Skin parameters 
  fss = 0.005 # fraction palm of hands (Sheridan et al., 1995, Rhodes et al., 2013)
  hsurf = 0.01 # applied layer thickness in cm, value is assumed to be 0.1 mm
  hsc = 0.0015 # Stratum corneum thickness (cm) # [Krüse 2007]
  hve = 0.0100 # Viable epidermis thickness (cm) # [Krüse 2007]
  hcell = 0.00005 # Blood vessel wall thickness (cm) # [Burton 1954] = 0.5 um capillary wall thickness
  ffatsc = 0.05 # Fraction of fat in stratum corneum [Polak 2012]
  ffatve = 0.02 # Fraction of fat in viable epidermis [Polak 2012]
  ffatepi = 0.02 # Fraction of fat in epidermis [Polak 2012]
  ffatbl = 0.007 # Fraction of fat in blood [Polak 2012]
  Kpcell = 0.93 # cm/h; Krüse model is normalized to 1 cm^2 # Permeability coefficient from arterial wall into the blood compartment [Krüse 2007]
  
  // PFOS
  
  //Physicochemical properties
  MW_PFOS = 500.13
  logP_PFOS = 4.49
  VP_PFOS = 0.27 # Pa; 1 mmHg = 133.322368 Pa; 0.002 mmHg (https://pubchem.ncbi.nlm.nih.gov/source/hsdb/7099#section=Environmental-Fate-%26-Exposure)
  
  Kscve_PFOS := ((1-ffatve)+(ffatve*(10^logP_PFOS)))/((1-ffatsc)+(ffatsc*(10^logP_PFOS)))
  Kver_PFOS := ((1-ffatbl)+(ffatbl*(10^logP_PFOS)))/((1-ffatepi)+(ffatepi*(10^logP_PFOS)))
  Kpsc_PFOS := 10^(0.74*logP_PFOS - 0.006*MW_PFOS - 2.8) # Stratum corneum permeability coefficient (cm/h) # [Polak 2012; KrÃ¼se 2007; Bunge and Cleek 1995; Cleek and Bunge 1993; Potts and Guy 1992]
  Kpve_PFOS := 2.6/(MW_PFOS^0.5) # Viable epidermis permeability coefficient (cm/h) # [Polak 2012 -> EPA 2004]
  Pab_PFOS := 1/(10^(6.96-(1.04*log10(VP_PFOS)) - 0.533*logP_PFOS - 0.00495*MW_PFOS))

  Tmc_PFOS = 84000 # 3500*24   # Maximum resorption rate 
  Kt_PFOS = 23   # Resorption affinity# same as monkey
  kurinec_PFOS = 0.024 # 0.001*24 # urinary elimination rate constant (/d/kg^-0.25)# estimated 
  Free_PFOS = 0.025  # Free fraction of PFOS in plasma# same as monkey 
  
  // Partition coefficients from Harada, et al 2005 
  PL_PFOS = 3.72   # Liver/plasma partition coefficient of PFOS
  PF_PFOS = 0.14   # Fat/plasma partition coefficient of PFOS
  PK_PFOS = 0.8   # Kidney/plasma partition coefficient of PFOS
  PSk_PFOS = 0.29   # Skin/plasma partition coefficient of PFOS
  PR_PFOS = 0.2   # Rest of the body/plasma partition coefficient of PFOS
  PG_PFOS = 0.57   # Gut/blood plasma coefficient of PFOS  
  PLun_PFOS = 0.15   # Lung/plasma partition coefficient of PFOS; [Fabrega 2014]

  // Free fraction of chemical in tissues 
  
  FreeL_PFOS := Free_PFOS/PL_PFOS;  # liver
  FreeF_PFOS := Free_PFOS/PF_PFOS;  # fat
  FreeK_PFOS := Free_PFOS/PK_PFOS;  # kidney
  FreeSk_PFOS := Free_PFOS/PSk_PFOS;  # skin
  FreeR_PFOS := Free_PFOS/PR_PFOS;  # rest of the body
  FreeG_PFOS := Free_PFOS/PG_PFOS;  # gut
  FreeLun_PFOS := Free_PFOS/PLun_PFOS;  # lung
  
  // Mother-child parameters - PFOS
  PT_PFOS= 0.36 # placenta transfer for PFOS, new add opinion 2020 
  Ratio_PFOS= 0.015 # milk concentration/maternal serum concentration for PFOS during breastfeeding, new add opinion 2020 
  DECLINE_PFOS = 0.031 # decline of PFOS in milk was 3.1% per month, new add opinion 2020 
  
  // Oral exposure via the mother
  PFOSMilkconcentration := PFOSmaternal*Ratio_PFOS # initial Milk concentration at birth ng/mL 
  Milkconsumption = 0.8 # milkconsumption L per day 
  Intakemilka_PFOS := PFOSMilkconcentration*Milkconsumption# initial intake via breastfeeding first month
  Intakemilkb_PFOS := Intakemilka_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeedingsecondmonth 
  Intakemilkc_PFOS := Intakemilkb_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding3month
  Intakemilkd_PFOS := Intakemilkc_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding4month 
  Intakemilke_PFOS := Intakemilkd_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding5month
  Intakemilkf_PFOS := Intakemilke_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding6month 
  Intakemilkg_PFOS := Intakemilkf_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding7month 
  Intakemilkh_PFOS := Intakemilkg_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding8month 
  Intakemilki_PFOS := Intakemilkh_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding9month 
  Intakemilkj_PFOS := Intakemilki_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding10month 
  Intakemilkk_PFOS := Intakemilkj_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding11month 
  Intakemilkl_PFOS := Intakemilkk_PFOS*(1-DECLINE_PFOS) #intakeviabreastfeeding12month
  
  CONCbirth_PFOS := PFOSmaternal*PT_PFOS # PFOS concentration at birth, new add in 2020 
  APlasbirth_PFOS := CONCbirth_PFOS*Free_PFOS*VPlasC*BWbirth # initial amount in plasma at birth 
  AGbirth_PFOS := APlasbirth_PFOS*9.112506818 # initial amount in gut at birth 
  ALbirth_PFOS := APlasbirth_PFOS*90.41427413 # initial amount in liver at birth 
  AFbirth_PFOS := APlasbirth_PFOS*27.99955778 # initial amount in fat at birth 
  AKbirth_PFOS := APlasbirth_PFOS*3.587821811 # initial amount in kidney at birth 
  ARbirth_PFOS := APlasbirth_PFOS*100.1289877 # initial amount in rest of the body at birth 
  Afilbirth_PFOS := APlasbirth_PFOS*0.0000150221 # initial amount filtrate at birth 
  Adelaybirth_PFOS := APlasbirth_PFOS*9.156014881 # initial amount in storage at birth 
  Aurinebirth_PFOS := APlasbirth_PFOS*565.1438095 # initial amount in urine at birth 
  
  // PFOS age-dependent starting values
  Aven_PFOS_background := APlasbirth_PFOS*0.67 # Background_PFAS_amounts$Aven_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Aart_PFOS_background := APlasbirth_PFOS*0.33 # Background_PFAS_amounts$Aart_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AG_PFOS_background := AGbirth_PFOS # Background_PFAS_amounts$AG_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AL_PFOS_background := ALbirth_PFOS # Background_PFAS_amounts$AL_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AF_PFOS_background := AFbirth_PFOS # Background_PFAS_amounts$AF_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AK_PFOS_background := AKbirth_PFOS # Background_PFAS_amounts$AK_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Afil_PFOS_background := Afilbirth_PFOS # Background_PFAS_amounts$Afil_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Adelay_PFOS_background := Adelaybirth_PFOS # Background_PFAS_amounts$Adelay_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Aurine_PFOS_background := Aurinebirth_PFOS # Background_PFAS_amounts$Aurine_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  ASk_PFOS_background = 0 # Background_PFAS_amounts$ASk_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Alun_PFOS_background = 0 # Background_PFAS_amounts$Alun_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AR_PFOS_background = 0.433 # Background_PFAS_amounts$AR_PFOS[which(Background_PFAS_amounts$year==round(AGE, digits=1))]

  // Time dependent variables
  BW := BWbirth + 4.47*age - 0.093*age^2 + 0.00061*age^3
  SkinTarea := 9.1*((BW*1000)^0.666)  # Total area of the skin (cm^2)
  kurine_PFOS := kurinec_PFOS*BW^(-0.25)
  Oraldose_PFOS_on := piecewise(Intakemilka_PFOS/BW, age < 0.083, Intakemilkb_PFOS/BW, age >= 0.083 && age < 0.167, Intakemilkc_PFOS/BW, age >= 0.167 && age < 0.250, Intakemilkd_PFOS/BW, age >= 0.250 && age < 0.333, Intakemilke_PFOS/BW, age >= 0.333 && age < 0.417, Intakemilkf_PFOS/BW, age >= 0.417 && age < 0.500, Intakemilkg_PFOS/BW,age >= 0.500 && age < 0.583,Intakemilkh_PFOS/BW,age >= 0.583 && age < 0.667,Intakemilki_PFOS/BW,age >= 0.667 && age < 0.750, Intakemilkj_PFOS/BW,age >= 0.750 && age < 0.833,Intakemilkk_PFOS/BW,age >= 0.833 && age < 0.917, Intakemilkl_PFOS/BW, age >= 0.917 && age < 1, Oralexpo_PFOS)  # (ug/day)
  Oraldose_PFOS := piecewise(Oraldose_PFOS_on*BW, day < exposure_duration, 0)
  Drinkdose_PFOS := piecewise((Drinkconc_PFOS*Drinkrate/1000)*BW, day < exposure_duration, 0)
  Inhalation_PFOS := piecewise(Cinh_PFOS_total_day, age >= age_start_inh_exp && age <= age_stop_inh_exp, 0)
                                                                                                                         
  // Scaled cardiac output and perfusion
  QC := QCC*BW^0.75;  # Cardiac output adjusted for BW (L/d)
  QCP := QC*(1-Htc) ; # Cardiac output adjusted for plasma flow (L/d)
  QL := QLC*QCP;  # Scaled plasma flow to liver (L/d)
  QF := QFC*QCP;  # Scaled plasma flow to fat (L/d)
  QK := QKC*QCP;  # Scaled plasma flow to kidney (L/d)
  Qfil := 0.2*QK      # Plasma flow to filtrate compartment (L/d)# 20% of QK 
  QG := QGC*QCP;  # Scaled plasma flow to gut (L/d)
  QSk := QSkC*QCP #*(Skinarea/SkinTarea); # scaled plasma flow to the skin
  QR := QCP-QL-QF-QK-QG-QSk; # Plasma flow to the rest of the body.
  Qbal := QCP - (QR+QL+QF+QK+QG+QSk)
  Qp := ((((1400-190)*(age)^2.5)/((age)^2.5 + 50)) + 190)*24 # L/d [ICRP 89] manual curve fit of age dependency
 
  // Scaled tissue volumes
  VL := VLC*BW;  # Scaled liver volume (L)
  VF := VFC*BW;  # Scaled fat volume (L)
  VK := VKC*BW;  # Scaled kidney volume (L)
  Vfil := VfilC*BW;  # Scaled filtrate compartment volume
  VG := VGC*BW;  # Gut volume (L)
  VPlas := VPlasC*BW;  # Scaled plasma volume(L)
  Vart_plas := 0.39*VPlas # [Brown 1997]
  Vven_plas := 0.61*VPlas # [Brown 1997]
  VSk := (Skinarea*Skinthickness)/1000;  # Skin volume (L)
  Vlun := VlunC*BW # Functional residual capacity # https://en.wikipedia.org/wiki
  VR := 0.84*BW-VL-VF-VK-Vfil-VG-VPlas-VSk-Vlun;  # Rest of the body volume (L). Need to know where the number 0.84 comes from???
  
  VT := 0.007*BW # Tidal volume # https://en.wikipedia.org/wiki/Tidal_volume
  
  //Functional_residual_capacity
  VFRC := 0.030*BW # Tidal volume
  
  Valv := VFRC + 0.5*VT # Alveolar volume
  VR := 0.84*BW - VL - VF - VK - Vfil - VG - VPlas - VSk - Vlun # Rest of the body volume (L) 
  Vbal := (0.84*BW)-(VL+VF+VK+Vfil+VG+VPlas+VSk+Vlun) # Balance check--better be 0 
  Vsurf := fss*SkinTarea*hsurf/1000 # volume of dermal layer on exposed skin surface (L) 1 m^2 = 10,000 cm^2 * cm = cm^3 = ml / 1000 = L
  
  Dermaldose_PFOS_day := Cdermal_PFOS_total_day*Vsurf

  // Dermal exposure
  Csurf_PFOS := piecewise(Cdermal_PFOS_total_day, age >= age_start_derm_exp && age <= age_stop_derm_exp, 0)
  Vsc := fss*SkinTarea*hsc/1000 # volume of exposed stratum corneum (L) cm^2 * cm = cm^3 = ml / 1000 = L
  Vve := fss*SkinTarea*hve/1000 # volume of exposed viable epidermis (L) cm^2 * cm = cm^3 = ml / 1000 = L
  
  CLsc_PFOS := 24*Kpsc_PFOS*fss*SkinTarea/1000 # stratum corneum clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  CLve_PFOS := 24*Kpve_PFOS*fss*SkinTarea/1000 # viable epidermis clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  
  CLcell := 24*Kpcell*fss*SkinTarea/1000 # clearance from arterial wall into the blood compartment (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  
  Tm_PFOS := Tmc_PFOS*BW^0.75 #transporter maximum 
  
  // Define compartments
  compartment Lung := Vlun # Lung
  compartment Trans = 0.0000001 # Transfer compartment
  compartment Sc # Stratum corneum
  compartment Ve := Vve # Viable epidermis
  compartment Skin := VSk;  # Skin compartment
  compartment Ven := Vven_plas
  compartment Art := Vart_plas 
  compartment Gut := VG;  # Gut compartment
  compartment Liv := VL;  # Liver compartment
  compartment Fat := VF;  # Fat compartment
  compartment Kid := VK;  # Kidney compartment
  compartment Fil := Vfil # kidney filtrate compartment
  compartment Delay;  # Storage compartment of urine
  compartment Urine;  # Urine compartment
  compartment Rest := VR # Rest compartment
  
  
  substanceOnly species Alung in Lung
  substanceOnly species Atrans in Trans
  substanceOnly species Asc in Sc
  substanceOnly species Ave in Ve
  substanceOnly species ASk in Skin
  substanceOnly species Aven in Ven
  substanceOnly species Aart in Art
  substanceOnly species AG in Gut
  substanceOnly species AL in Liv
  substanceOnly species AF in Fat
  substanceOnly species AK in Kid
  substanceOnly species Afil in Fil
  substanceOnly species Adelay in Delay
  substanceOnly species Aurine in Urine
  substanceOnly species AR in Rest
  
  Alung = Alun_PFOS_background
  Atrans = 0
  Asc = 0
  Ave = 0
  ASk = ASk_PFOS_background
  Aven = Aven_PFOS_background
  Aart = Aart_PFOS_background
  AG = AG_PFOS_background # amount of PFOS in gut
  AL = AL_PFOS_background;   # amount of PFOS in liver
  AF = AF_PFOS_background;   # amount of PFOS in fat
  AK = AK_PFOS_background;   # amount of PFOS in kidney
  Afil = Afil_PFOS_background; # amount of PFOS in kidney filtrate
  Adelay = Adelay_PFOS_background;  # amount of PFOS in storage compartment of urine
  Aurine = Aurine_PFOS_background;  # amount of PFOS in urine
  AR = AR_PFOS_background;     # amount of PFOS in the rest of the body
  
  // Transfer equations
  
  // Exposure
  -> Alung; Qp*Inhalation_PFOS # Inhalation
  -> AG; Oraldose_PFOS + Drinkdose_PFOS # Oral
  -> Asc; 0 # dermal dose not supported yet
  
  Asc -> Atrans; 2*CLsc_PFOS*(Asc / Vsc)
  Atrans -> Ave; 2*CLve_PFOS*((Atrans / 0.0000001)/Kscve_PFOS)
  Atrans -> Asc; 2*CLsc_PFOS*(Atrans / 0.0000001)
  Ave -> Atrans; 2*CLve_PFOS*(Ave / Vve)
  Ave -> ASk; CLcell*(Ave / Vve)/Kver_PFOS
  
  Alung -> Aart; QCP*(Alung / (Valv*Pab_PFOS + Vlun))
  Aart -> AG; QG*(Aart/Vart_plas)
  Aart -> AL; QL*(Aart/Vart_plas)
  Aart -> AF; QF*(Aart/Vart_plas)
  Aart -> AK; QK*(Aart/Vart_plas)
  Aart -> Afil; Qfil*(Aart/Vart_plas)
  Aart -> AR; QR*(Aart/Vart_plas)
  Aart -> ASk; QSk*(Aart/Vart_plas)
  
  AG -> AL; QG*FreeG_PFOS*(AG/VG)
  Afil -> AK; (Tm_PFOS*(Afil/Vfil))/(Kt_PFOS+(Afil/Vfil))
  Afil -> Adelay; Qfil*(Afil/Vfil)
  Adelay -> Aurine; kurine_PFOS*Adelay 
  
  AL -> Aven; (QL+QG)*FreeL_PFOS*(AL/VL)
  AF -> Aven; QF*FreeF_PFOS*(AF/VF)
  AK -> Aven; QK*FreeK_PFOS*(AK/VK)
  AR -> Aven; QR*FreeR_PFOS*(AR/VR)
  ASk -> Aven; QSk*FreeSk_PFOS*(ASk/VSk)
  
  Aven -> Alung; QCP*(Aven/Vven_plas)
  
end

