function conc(q,vol)
  piecewise(q/vol, vol > 1e-5, 0)
end

// Westerhout et al., 2024 PFOA PBPK model


model PFOA()
  
  // TIMING
  age_start = 20 # occupational exposure starting age
  age_stop = 60 # occupational exposure stopping age
  
  day := time + 1
  dayoftheweek := (time + 1) % 7
  month := ceiling(day/(365/12))-(((ceiling(day/365))-1)*12)
  year := ceiling(day/(365))
  age := time / 365 # used as a starting point, rather than birth [years]
  
  // EXPOSURE PARAMETERS
  exposure_duration = 18750 # Duration of exposure (d)
  
  // Drinking water exposure 
  Drinkrate = 13 # Drinking water rate (mL/kg/day)
  
  // Dermal exposure 
  Dermexpo = 0        # 0 = NO, 1 = YES
  Dermconc = 0.0      # Dermal concentration (mg/mL) 
  Dermvol = 0.001     # Dermal exposure volume (mL)
  Dermdose := Dermconc*Dermvol*1000 # (ug) 
  Skinarea = 972      # Exposed area on skin (cm^2) 
  Skinthickness = 0.1 # Skin thickness (cm) 
  
  // Oral exposure via the mother
  Oralexpo_PFOA = 0.000187 # µg/kg/day TWI of 4.4 ng/kg/wk assuming PFOA:PFOS ratio of 24:76 from [EFSA opinion 2020, p.152]
  Drinkconc_PFOA = 0 # Drinking water concentration (ug/L or ppb) 
  PFOAmaternal = 2.0 # maternal concentration ng/mL at delivery [EFSA opinion 2020, p.152]
  
  // Inhalation exposure
  Cinh_PFOA = 0 # ug/L; 0.526 ug/m3 [Nilsson 2013]
  Cinh_82FTOH = 0 # ug/L; 114 ug/m3 [Nilsson 2013]
  Biotransformation = 0.003 # 8:2-FTOH to PFOA [Gomis 2016]
  Cinh_PFOA_total_day := Cinh_PFOA/4 + (Cinh_82FTOH*Biotransformation)/4 # 6h exposure spread across a 24h day
  
  // Dermal exposure
  Cdermal_PFOA = 0 # ug/kg = 0.680 ug/g [Freberg 2010]
  Cdermal_PFOA_total_day := Cdermal_PFOA/4 # 6h exposure spread across a 24h day
  
  // PHYSIOLOGY
  
  // Perfusion fractions
  QCC = 300;     # 12.5*24   # Cardiac blood output (L/d/kg^0.75)
  QFC = 0.052;   # Fraction cardiac output going to fat
  QLC = 0.069;   # Fraction cardiac output going to liver suggested by EFSA is 0.069, since they corrected for output to the gut Luccisano used 0.25, but Brown actually report 0.23
  QKC = 0.175;   # Fraction cardiac output going to kidney
  QSkC = 0.058;  # Fraction cardiac output going to skin
  QGC = 0.181;   # Fraction of cardiac output going to gut and the liver via portal arthery
  
  // Fraction tissue volumes
  VLC = 0.026;   # Fraction liver volume
  VFC = 0.214;   # Fraction fat volume
  VKC = 0.004;   # Fraction kidney volume
  VfilC = 0.0004;# Fraction filtrate compartment volume (10% of kidney volume)
  VGC = 0.0171;  # Fraction gut volume
  VlunC = 0.007 # Fraction lung volume (ICRP 89)
  VPlasC = 0.0428;  # Fraction plasma volume (58% of blood)
  Htc = 0.44;  # hematocrit
  
  // Mother-child parameters
  BWbirth = 3.68 # Body weight at birth in kg new add opinion 2020 
  
  // Skin parameters 
  fss = 0.005 # fraction palm of hands (Sheridan et al., 1995, Rhodes et al., 2013)
  hsurf = 0.01 # applied layer thickness in cm, value is assumed to be 0.1 mm
  hsc = 0.0015 # Stratum corneum thickness (cm) # [Krüse 2007]
  hve = 0.0100 # Viable epidermis thickness (cm) # [Krüse 2007]
  hcell = 0.00005 # Blood vessel wall thickness (cm) # [Burton 1954] = 0.5 um capillary wall thickness
  ffatsc = 0.05 # Fraction of fat in stratum corneum [Polak 2012]
  ffatve = 0.02 # Fraction of fat in viable epidermis [Polak 2012]
  ffatepi = 0.02 # Fraction of fat in epidermis [Polak 2012]
  ffatbl = 0.007 # Fraction of fat in blood [Polak 2012]
  Kpcell = 0.93 # cm/h; Krüse model is normalized to 1 cm^2 # Permeability coefficient from arterial wall into the blood compartment [Krüse 2007]
  
  // PFOA
  
  //Physicochemical properties
  MW_PFOA = 414.07
  logP_PFOA = 4.81
  VP_PFOA = 2.34 # 10^0.37 # Pa [Zhang 2021]; 1 mmHg = 133.322368 Pa; 0.15 mmHg (https://haz-map.com/Agents/6596) = 20 Pa
  
  Kscve_PFOA := ((1-ffatve)+(ffatve*(10^logP_PFOA)))/((1-ffatsc)+(ffatsc*(10^logP_PFOA)))
  Kver_PFOA := ((1-ffatbl)+(ffatbl*(10^logP_PFOA)))/((1-ffatepi)+(ffatepi*(10^logP_PFOA)))
  Kpsc_PFOA = 0.000088 # [Franko 2011]
  Kpve_PFOA = 0.000088 # [Franko 2011]
  Pab_PFOA := 1/(10^(6.96-(1.04*log10(VP_PFOA)) - 0.533*logP_PFOA - 0.00495*MW_PFOA))

  Tmc_PFOA = 144000 # 6000*24   # Maximum resorption rate 
  Kt_PFOA = 55   # Resorption affinity# same as monkey
  kurinec_PFOA = 0.0072 # 0.0003*24 # urinary elimination rate constant (/d/kg^-0.25)# estimated 
  Free_PFOA = 0.02  # Free fraction of PFOA in plasma# same as monkey 
  
  // Partition coefficients from Harada, et al 2005 
  PL_PFOA = 2.2   # Liver/plasma partition coefficient of PFOA
  PF_PFOA = 0.04   # Fat/plasma partition coefficient of PFOA
  PK_PFOA = 1.05   # Kidney/plasma partition coefficient of PFOA
  PSk_PFOA = 0.1   # Skin/plasma partition coefficient of PFOA
  PR_PFOA = 0.12   # Rest of the body/plasma partition coefficient of PFOA
  PG_PFOA = 0.05   # Gut/blood plasma coefficient of PFOA  
  PLun_PFOA = 1.27   # Lung/plasma partition coefficient of PFOA; [Fabrega 2014]

  // Free fraction of chemical in tissues 
  
  FreeL_PFOA := Free_PFOA/PL_PFOA;  # liver
  FreeF_PFOA := Free_PFOA/PF_PFOA;  # fat
  FreeK_PFOA := Free_PFOA/PK_PFOA;  # kidney
  FreeSk_PFOA := Free_PFOA/PSk_PFOA;  # skin
  FreeR_PFOA := Free_PFOA/PR_PFOA;  # rest of the body
  FreeG_PFOA := Free_PFOA/PG_PFOA;  # gut
  FreeLun_PFOA := Free_PFOA/PLun_PFOA;  # lung
  
  // Mother-child parameters - PFOA
  PT_PFOA= 0.74 # placenta transfer for PFOA, new add opinion 2020 
  Ratio_PFOA= 0.03 # milk concentration/maternal serum concentration for PFOA during breastfeeding, new add opinion 2020 
  DECLINE_PFOA = 0.077 # decline of PFOA in milk was 7.7% per month, new add opinion 2020 
  
  // Oral exposure via the mother
  PFOaMilkconcentration := PFOAmaternal*Ratio_PFOA # initial Milk concentration at birth ng/mL 
  Milkconsumption = 0.8 # milkconsumption L per day 
  Intakemilka_PFOA := PFOaMilkconcentration*Milkconsumption# initial intake via breastfeeding first month
  Intakemilkb_PFOA := Intakemilka_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeedingsecondmonth 
  Intakemilkc_PFOA := Intakemilkb_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding3month
  Intakemilkd_PFOA := Intakemilkc_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding4month 
  Intakemilke_PFOA := Intakemilkd_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding5month
  Intakemilkf_PFOA := Intakemilke_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding6month 
  Intakemilkg_PFOA := Intakemilkf_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding7month 
  Intakemilkh_PFOA := Intakemilkg_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding8month 
  Intakemilki_PFOA := Intakemilkh_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding9month 
  Intakemilkj_PFOA := Intakemilki_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding10month 
  Intakemilkk_PFOA := Intakemilkj_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding11month 
  Intakemilkl_PFOA := Intakemilkk_PFOA*(1-DECLINE_PFOA) #intakeviabreastfeeding12month
  
  CONCbirth_PFOA := PFOAmaternal*PT_PFOA # PFOA concentration at birth, new add in 2020 
  APlasbirth_PFOA := CONCbirth_PFOA*Free_PFOA*VPlasC*BWbirth # initial amount in plasma at birth 
  AGbirth_PFOA := APlasbirth_PFOA*0.99929853 # initial amount in gut at birth 
  ALbirth_PFOA := APlasbirth_PFOA*66.8360764 # initial amount in liver at birth 
  AFbirth_PFOA := APlasbirth_PFOA*10.000151 # initial amount in fat at birth 
  AKbirth_PFOA := APlasbirth_PFOA*5.88575746 # initial amount in kidney at birth 
  ARbirth_PFOA := APlasbirth_PFOA*74.9108792 # initial amount in rest of the body at birth 
  Afilbirth_PFOA := APlasbirth_PFOA*0.0000209606 # initial amount filtrate at birth 
  Adelaybirth_PFOA := APlasbirth_PFOA*42.27910714 # initial amount in storage at birth 
  Aurinebirth_PFOA := APlasbirth_PFOA*769.1347494 # initial amount in urine at birth 
  
  // PFOA age-dependent starting values
  Aven_PFOA_background := APlasbirth_PFOA*0.67 # Background_PFAS_amounts$Aven_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Aart_PFOA_background := APlasbirth_PFOA*0.33 # Background_PFAS_amounts$Aart_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AG_PFOA_background := AGbirth_PFOA # Background_PFAS_amounts$AG_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AL_PFOA_background := ALbirth_PFOA # Background_PFAS_amounts$AL_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AF_PFOA_background := AFbirth_PFOA # Background_PFAS_amounts$AF_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AK_PFOA_background := AKbirth_PFOA # Background_PFAS_amounts$AK_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Afil_PFOA_background := Afilbirth_PFOA # Background_PFAS_amounts$Afil_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Adelay_PFOA_background := Adelaybirth_PFOA # Background_PFAS_amounts$Adelay_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Aurine_PFOA_background := Aurinebirth_PFOA # Background_PFAS_amounts$Aurine_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  ASk_PFOA_background = 0 # Background_PFAS_amounts$ASk_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  Alun_PFOA_background = 0 # Background_PFAS_amounts$Alun_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]
  AR_PFOA_background = 0.15 # Background_PFAS_amounts$AR_PFOA[which(Background_PFAS_amounts$year==round(AGE, digits=1))]

  
  // Time dependent variables
  BW := BWbirth + 4.47*age - 0.093*age^2 + 0.00061*age^3
  SkinTarea := 9.1*((BW*1000)^0.666)  # Total area of the skin (cm^2)
  kurine_PFOA := kurinec_PFOA*BW^(-0.25)
  Oraldose_PFOA_on := piecewise(Intakemilka_PFOA/BW, age < 0.083, Intakemilkb_PFOA/BW, age >= 0.083 && age < 0.167, Intakemilkc_PFOA/BW, age >= 0.167 && age < 0.250, Intakemilkd_PFOA/BW, age >= 0.250 && age < 0.333, Intakemilke_PFOA/BW, age >= 0.333 && age < 0.417, Intakemilkf_PFOA/BW, age >= 0.417 && age < 0.500, Intakemilkg_PFOA/BW,age >= 0.500 && age < 0.583,Intakemilkh_PFOA/BW,age >= 0.583 && age < 0.667,Intakemilki_PFOA/BW,age >= 0.667 && age < 0.750, Intakemilkj_PFOA/BW,age >= 0.750 && age < 0.833,Intakemilkk_PFOA/BW,age >= 0.833 && age < 0.917, Intakemilkl_PFOA/BW, age >= 0.917 && age < 1, Oralexpo_PFOA)  # (ug/day)
  Oraldose_PFOA := piecewise(Oraldose_PFOA_on*BW, day < exposure_duration, 0)
  Drinkdose_PFOA := piecewise((Drinkconc_PFOA*Drinkrate/1000)*BW, day < exposure_duration, 0)
  Inhalation_PFOA := piecewise(Cinh_PFOA_total_day, dayoftheweek >= 1 && dayoftheweek <= 5 && age >= age_start && age <= age_stop && month >= 5 && month <= 10, 0)
                                                                                                                         
  // Scaled cardiac output and perfusion
  QC := QCC*BW^0.75;  # Cardiac output adjusted for BW (L/d)
  QCP := QC*(1-Htc) ; # Cardiac output adjusted for plasma flow (L/d)
  QL := QLC*QCP;  # Scaled plasma flow to liver (L/d)
  QF := QFC*QCP;  # Scaled plasma flow to fat (L/d)
  QK := QKC*QCP;  # Scaled plasma flow to kidney (L/d)
  Qfil := 0.2*QK      # Plasma flow to filtrate compartment (L/d)# 20% of QK 
  QG := QGC*QCP;  # Scaled plasma flow to gut (L/d)
  QSk := QSkC*QCP #*(Skinarea/SkinTarea); # scaled plasma flow to the skin
  QR := QCP-QL-QF-QK-QG-QSk; # Plasma flow to the rest of the body.
  Qbal := QCP - (QR+QL+QF+QK+QG+QSk)
  Qp := ((((1400-190)*(age)^2.5)/((age)^2.5 + 50)) + 190)*24 # L/d [ICRP 89] manual curve fit of age dependency
 
  // Scaled tissue volumes
  VL := VLC*BW;  # Scaled liver volume (L)
  VF := VFC*BW;  # Scaled fat volume (L)
  VK := VKC*BW;  # Scaled kidney volume (L)
  Vfil := VfilC*BW;  # Scaled filtrate compartment volume
  VG := VGC*BW;  # Gut volume (L)
  VPlas := VPlasC*BW;  # Scaled plasma volume(L)
  Vart_plas := 0.39*VPlas # [Brown 1997]
  Vven_plas := 0.61*VPlas # [Brown 1997]
  VSk := (Skinarea*Skinthickness)/1000;  # Skin volume (L)
  Vlun := VlunC*BW # Functional residual capacity # https://en.wikipedia.org/wiki
  VR := 0.84*BW-VL-VF-VK-Vfil-VG-VPlas-VSk-Vlun;  # Rest of the body volume (L). Need to know where the number 0.84 comes from???
  
  VT = 0.007*BW # Tidal volume # https://en.wikipedia.org/wiki/Tidal_volume
  
  //Functional_residual_capacity
  VFRC := 0.030*BW # Tidal volume
  
  Valv := VFRC + 0.5*VT # Alveolar volume
  VR := 0.84*BW - VL - VF - VK - Vfil - VG - VPlas - VSk - Vlun # Rest of the body volume (L) 
  Vbal := (0.84*BW)-(VL+VF+VK+Vfil+VG+VPlas+VSk+Vlun) # Balance check--better be 0 
  Vsurf := fss*SkinTarea*hsurf/1000 # volume of dermal layer on exposed skin surface (L) 1 m^2 = 10,000 cm^2 * cm = cm^3 = ml / 1000 = L
  
  Dermaldose_PFOA_day = Cdermal_PFOA_total_day*Vsurf

  // Dermal exposure
  Csurf_PFOA = 0
  Vsc := fss*SkinTarea*hsc/1000 # volume of exposed stratum corneum (L) cm^2 * cm = cm^3 = ml / 1000 = L
  Vve := fss*SkinTarea*hve/1000 # volume of exposed viable epidermis (L) cm^2 * cm = cm^3 = ml / 1000 = L
  
  CLsc_PFOA := 24*Kpsc_PFOA*fss*SkinTarea/1000 # stratum corneum clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  CLve_PFOA := 24*Kpve_PFOA*fss*SkinTarea/1000 # viable epidermis clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  
  CLcell := 24*Kpcell*fss*SkinTarea/1000 # clearance from arterial wall into the blood compartment (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  
  Tm_PFOA := Tmc_PFOA*BW^0.75 #transporter maximum 
  
  // Define compartments
  compartment Lung := Vlun # Lung
  compartment Trans = 0.0000001 # Transfer compartment
  compartment Sc # Stratum corneum
  compartment Ve := Vve # Viable epidermis
  compartment Skin := VSk;  # Skin compartment
  compartment Ven := Vven_plas
  compartment Art := Vart_plas 
  compartment Gut := VG;  # Gut compartment
  compartment Liv := VL;  # Liver compartment
  compartment Fat := VF;  # Fat compartment
  compartment Kid := VK;  # Kidney compartment
  compartment Fil := Vfil # kidney filtrate compartment
  compartment Delay;  # Storage compartment of urine
  compartment Urine;  # Urine compartment
  compartment Rest := VR # Rest compartment
  
  
  substanceOnly species Alung in Lung
  substanceOnly species Atrans in Trans
  substanceOnly species Asc in Sc
  substanceOnly species Ave in Ve
  substanceOnly species ASk in Skin
  substanceOnly species Aven in Ven
  substanceOnly species Aart in Art
  substanceOnly species AG in Gut
  substanceOnly species AL in Liv
  substanceOnly species AF in Fat
  substanceOnly species AK in Kid
  substanceOnly species Afil in Fil
  substanceOnly species Adelay in Delay
  substanceOnly species Aurine in Urine
  substanceOnly species AR in Rest
  
  Alung = Alun_PFOA_background
  Atrans = 0
  Asc = 0
  Ave = 0
  ASk = ASk_PFOA_background
  Aven = Aven_PFOA_background
  Aart = Aart_PFOA_background
  AG = AG_PFOA_background # amount of PFOA in gut
  AL = AL_PFOA_background;   # amount of PFOA in liver
  AF = AF_PFOA_background;   # amount of PFOA in fat
  AK = AK_PFOA_background;   # amount of PFOA in kidney
  Afil = Afil_PFOA_background; # amount of PFOA in kidney filtrate
  Adelay = Adelay_PFOA_background;  # amount of PFOA in storage compartment of urine
  Aurine = Aurine_PFOA_background;  # amount of PFOA in urine
  AR = AR_PFOA_background;     # amount of PFOA in the rest of the body
  
  // Transfer equations
  
  // Exposure
  -> Alung; Qp*Inhalation_PFOA # Inhalation
  -> AG; Oraldose_PFOA + Drinkdose_PFOA # Oral
  -> Asc; 0 # dermal dose not supported yet
  
  Asc -> Atrans; 2*CLsc_PFOA*(Asc / Vsc)
  Atrans -> Ave; 2*CLve_PFOA*((Atrans / 0.0000001)/Kscve_PFOA)
  Atrans -> Asc; 2*CLsc_PFOA*(Atrans / 0.0000001)
  Ave -> Atrans; 2*CLve_PFOA*(Ave / Vve)
  Ave -> ASk; CLcell*(Ave / Vve)/Kver_PFOA
  
  Alung -> Aart; QCP*(Alung / (Valv*Pab_PFOA + Vlun))
  Aart -> AG; QG*(Aart/Vart_plas)
  Aart -> AL; QL*(Aart/Vart_plas)
  Aart -> AF; QF*(Aart/Vart_plas)
  Aart -> AK; QK*(Aart/Vart_plas)
  Aart -> Afil; Qfil*(Aart/Vart_plas)
  Aart -> AR; QR*(Aart/Vart_plas)
  Aart -> ASk; QSk*(Aart/Vart_plas)
  
  AG -> AL; QG*FreeG_PFOA*(AG/VG)
  Afil -> AK; (Tm_PFOA*(Afil/Vfil))/(Kt_PFOA+(Afil/Vfil))
  Afil -> Adelay; Qfil*(Afil/Vfil)
  Adelay -> Aurine; kurine_PFOA*Adelay 
  
  AL -> Aven; (QL+QG)*FreeL_PFOA*(AL/VL)
  AF -> Aven; QF*FreeF_PFOA*(AF/VF)
  AK -> Aven; QK*FreeK_PFOA*(AK/VK)
  AR -> Aven; QR*FreeR_PFOA*(AR/VR)
  ASk -> Aven; QSk*FreeSk_PFOA*(ASk/VSk)
  
  Aven -> Alung; QCP*(Aven/Vven_plas)
  
end

