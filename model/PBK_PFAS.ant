// Based on Westerhout et al., 2024 PFOA PBPK model

model PBK_PFAS()

  // PHYSIOLOGY
  BW = 70              # Body weight (perhaps make time dependent: input age and bw-at-age)

  // Perfusion fractions
  QCC = 300            # Cardiac blood output (L/d/kg^0.75) (12.5*24)
  QFC = 0.052          # Fraction cardiac output going to fat
  QLC = 0.069          # Fraction cardiac output going to liver
  QKC = 0.175          # Fraction cardiac output going to kidney
  QfilC = 0.2		       # Fraction of kidney plasma flow to filtrate
  QSkC = 0.058         # Fraction cardiac output going to skin
  QGC = 0.181          # Fraction of cardiac output going to gut and the liver via portal arthery

  // Fraction tissue volumes
  FBW = 0.84           # Fraction of the BW covered by the sum of the compartments (used for the calculation of the rest compartment)
  VLC = 0.026          # Fraction liver volume
  VFC = 0.214          # Fraction fat volume
  VKC = 0.004          # Fraction kidney volume
  VfilC = 0.0004       # Fraction filtrate compartment volume (10% of kidney volume)
  VGC = 0.0171         # Fraction gut volume
  VlunC = 0.007        # Fraction lung volume (ICRP 89)
  VPlasC = 0.0428      # Fraction plasma volume (58% of blood)
  VartC = 0.39         # Fraction of plasma in arteries
  VvenC = 0.61     	   # Fraction of plasma in venes
  Htc = 0.44           # Hematocrit
  
  // Skin parameters
  fss = 0.05           # fraction of the skin exposed
  Meeh_A = 910         # Meeh-type equation: BSA = A*BW^B, coefficient A (cm^2/kg^0.666)
  Meeh_B = 0.666       # Meeh-type equation: BSA = A*BW^B, coefficient B
  SkinTarea := Meeh_A*BW^Meeh_B  # Total area of the skin (cm^2)
  Skinthickness = 0.1  # Skin thickness (cm)
  Skinarea := fss*SkinTarea       # Exposed area on skin (cm^2)
  
  // Physicochemical properties
  MW = 414.07
  logP = 4.81
  VP = 2.34            # Vapor pressure (Pa) 10^0.37 [Zhang 2021]; 1 mmHg = 133.322368 Pa; 0.15 mmHg (https://haz-map.com/Agents/6596) = 20 Pa

  Tmc = 144000         # 6000*24 # Maximum resorption rate (ug/d/kg^.75)^a (based on half-life of 2.3 years in humans)
  Kt = 55              # Resorption affinity (ug/L) (same as monkey)
  kurinec = 0.0072     # 0.0003*24 # urinary elimination rate constant (/d/kg^-0.25)# estimated
  Free = 0.02          # Free fraction in plasma (same as monkey)

  // Inhalation exposure
  Biotransformation = 0.003 # 8:2-FTOH to PFOA [Gomis 2016]

  // Partition coefficients from Harada, et al 2005
  PL = 2.2             # Liver/plasma partition coefficient
  PF = 0.04            # Fat/plasma partition coefficient
  PK = 1.05            # Kidney/plasma partition coefficient
  PSk = 0.1            # Skin/plasma partition coefficient
  PR = 0.12            # Rest of the body/plasma partition coefficient
  PG = 0.05            # Gut/blood plasma coefficient
  PLun = 1.27          # Lung/plasma partition coefficient; [Fabrega 2014]

  // Assignments
  kurine := kurinec*BW^(-0.25)
  Tm := Tmc*BW^0.75    # transporter maximum (ug/day)

  // Free fraction of chemical in tissues
  FreeL := Free/PL             # Free fraction in liver
  FreeF := Free/PF             # Free fraction in fat
  FreeK := Free/PK             # Free fraction in kidney
  FreeSk := Free/PSk           # Free fraction in skin
  FreeR := Free/PR             # Free fraction in rest-of-body
  FreeG := Free/PG             # Free fraction in gut
  FreeLun := Free/PLun         # Free fraction in lung

  // Scaled cardiac output and perfusion
  QC := QCC*BW^0.75            # Cardiac output adjusted for BW (L/d)
  QCP := QC*(1-Htc)            # Cardiac output adjusted for plasma flow (L/d)
  QL := QLC*QCP                # Scaled plasma flow to liver (L/d)
  QF := QFC*QCP                # Scaled plasma flow to fat (L/d)
  QK := QKC*QCP                # Scaled plasma flow to kidney (L/d)
  Qfil := QfilC*QK             # Plasma flow to filtrate compartment (L/d)# 20% of QK
  QG := QGC*QCP                # Scaled plasma flow to gut (L/d)
  QSk := QSkC*QCP              # scaled plasma flow to the skin
  QR := QCP-QL-QF-QK-QG-QSk    # Plasma flow to the rest of the body

  // Define compartments
  compartment Lung := VlunC*BW                        # Lung
  compartment Skin := (Skinarea*Skinthickness)/1000   # Skin compartment
  compartment Ven_Plas := VvenC*VPlasC*BW             # Venous plasma
  compartment Art_Plas := VartC*VPlasC*BW             # Arterial plasma
  compartment Gut := VGC*BW                           # Gut compartment
  compartment Liv := VLC*BW                           # Liver compartment
  compartment Fat := VFC*BW                           # Fat compartment
  compartment Kid := VKC*BW                           # Kidney compartment
  compartment Fil := VfilC*BW                         # kidney filtrate compartment
  compartment Delay                                   # Storage compartment of urine
  compartment Urine                                   # Urine compartment
  compartment Rest := (FBW-VLC-VFC-VKC-VfilC-VGC-VPlasC-VlunC)*BW - Skin  # Rest compartment

  // Define species
  substanceOnly species Alung in Lung          # amount in lung
  substanceOnly species ASk in Skin            # amount in skin
  substanceOnly species AVen_Plas in Ven_Plas  # amount in venous plasma
  substanceOnly species AArt_Plas in Art_Plas  # amount in arterial plasma
  substanceOnly species AG in Gut              # amount in gut
  substanceOnly species AL in Liv              # amount in liver
  substanceOnly species AF in Fat              # amount in fat
  substanceOnly species AK in Kid              # amount in kidney
  substanceOnly species Afil in Fil            # amount in kidney filtrate
  substanceOnly species Adelay in Delay        # amount in storage compartment of urine
  substanceOnly species Aurine in Urine        # amount in urine
  substanceOnly species AR in Rest             # amount in the rest of the body

  // Initial amounts
  Alung = 0
  ASk = 0
  AVen_Plas = 0
  AArt_Plas = 0
  AG = 0
  AL = 0
  AF = 0
  AK = 0
  Afil = 0
  Adelay = 0
  Aurine = 0
  AR = 0

  // Transfer equations
  Alung -> AArt_Plas; QCP*FreeLun*(Alung/Lung)
  AArt_Plas -> AG; QG*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> AL; QL*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> AF; QF*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> AK; QK*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> Afil; Qfil*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> AR; QR*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> ASk; QSk*Free*(AArt_Plas/Art_Plas)

  AG -> AL; QG*FreeG*(AG/Gut)
  Afil -> AK; (Tm*(Afil/Fil))/(Kt+(Afil/Fil))
  Afil -> Adelay; Qfil*(Afil/Fil)
  Adelay -> Aurine; kurine*Adelay

  AL -> AVen_Plas; (QL+QG)*FreeL*(AL/Liv)
  AF -> AVen_Plas; QF*FreeF*(AF/Fat)
  AK -> AVen_Plas; QK*FreeK*(AK/Kid)
  AR -> AVen_Plas; QR*FreeR*(AR/Rest)
  ASk -> AVen_Plas; QSk*FreeSk*(ASk/Skin)

  AVen_Plas -> Alung; QCP*Free*(AVen_Plas/Ven_Plas)

end
