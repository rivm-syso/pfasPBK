# Generic PFAS PBK model implementation in Antimony based on
# the PFOA PBK model of Westerhout et al. (2024).

model PBK_PFAS()

  # Parameters

  ## Physiology
  BW = 70              # Body weight [kg]

  ### Perfusion fractions
  QCC = 300            # Cardiac blood output [L/d/kg^0.75] (12.5*24)
  QFC = 0.052          # Fraction cardiac output going to fat [-]
  QLC = 0.069          # Fraction cardiac output going to liver [-]
  QKC = 0.175          # Fraction cardiac output going to kidney [-]
  QfilC = 0.2          # Fraction of kidney plasma flow to filtrate [-]
  QSkC = 0.058         # Fraction cardiac output going to skin [-]
  QGC = 0.181          # Fraction of cardiac output going to gut and the liver via portal arthery [-]

  ### Fraction tissue volumes
  FBW = 0.84           # Fraction of the BW covered by the sum of the compartments [-]
  VLC = 0.026          # Fraction liver volume [-]
  VFC = 0.214          # Fraction fat volume [-]
  VKC = 0.004          # Fraction kidney volume [-]
  VfilC = 0.0004       # Fraction filtrate compartment volume [-] (10% of kidney volume)
  VGC = 0.0171         # Fraction gut volume [-]
  VlunC = 0.007        # Fraction lung volume [-] (ICRP 89)
  VPlasC = 0.0428      # Fraction plasma volume[-] (58% of blood)
  VartC = 0.39         # Fraction of plasma in arteries [-]
  VvenC = 0.61         # Fraction of plasma in venes [-]
  Htc = 0.44           # Hematocrit [-]

  ### Skin parameters
  BSA := 9.1*((BW*1000)^0.666)  # Total area of the skin [cm^2]
  SkinThickness = 0.1           # Skin thickness [cm]
  FSkinExposed = 0.05           # Fraction of the skin exposed [-]

  ## Physicochemical properties
  MW = 414.07
  logP = 4.81
  VP = 2.34            # Vapor pressure [Pa] (10^0.37 [Zhang 2021]; 1 mmHg = 133.322368 Pa; 0.15 mmHg = 20 Pa)

  ## Biochemical parameters
  Tmc = 144000         # Maximum resorption rate [ug/d/kg^.75] (6000 * 24, based on half-life of 2.3 years in humans)
  Kt = 55              # Resorption affinity [ug/L] (same as monkey)
  kurinec = 0.0072     # Urinary elimination rate constant [/d/kg^-0.25] (0.0003*24)
  Free = 0.02          # Free fraction in plasma [-] (same as monkey)

  ### Partition coefficients
  PL = 2.2             # Liver/plasma partition coefficient [-] (Harada et al. 2005)
  PF = 0.04            # Fat/plasma partition coefficient [-] (Harada et al. 2005)
  PK = 1.05            # Kidney/plasma partition coefficient [-] (Harada et al. 2005)
  PSk = 0.1            # Skin/plasma partition coefficient [-] (Harada et al. 2005)
  PR = 0.12            # Rest of the body/plasma partition coefficient [-] (Harada et al. 2005)
  PG = 0.05            # Gut/blood plasma coefficient [-] (Harada et al. 2005)
  PLun = 1.27          # Lung/plasma partition coefficient [-] (Fabrega et al. 2014)

  # Assignments
  kurine := kurinec*BW^(-0.25) # Urinary elimination rate constant [/d]
  Tm := Tmc*BW^0.75            # Transporter maximum [ug/day]

  ## Free fraction of chemical in tissues
  FreeL := Free/PL             # Free fraction in liver
  FreeF := Free/PF             # Free fraction in fat
  FreeK := Free/PK             # Free fraction in kidney
  FreeSk := Free/PSk           # Free fraction in skin
  FreeR := Free/PR             # Free fraction in rest-of-body
  FreeG := Free/PG             # Free fraction in gut
  FreeLun := Free/PLun         # Free fraction in lung

  ## Scaled cardiac output and perfusion
  QC := QCC*BW^0.75            # Cardiac output adjusted for BW [L/d]
  QCP := QC*(1-Htc)            # Cardiac output adjusted for plasma flow [L/d]
  QL := QLC*QCP                # Scaled plasma flow to liver [L/d]
  QF := QFC*QCP                # Scaled plasma flow to fat [L/d]
  QK := QKC*QCP                # Scaled plasma flow to kidney [L/d]
  Qfil := QfilC*QK             # Plasma flow to filtrate compartment [L/d] (20% of QK)
  QG := QGC*QCP                # Scaled plasma flow to gut [L/d]
  QSk := QSkC*QCP              # scaled plasma flow to the skin [L/d]
  QR := QCP-QL-QF-QK-QG-QSk    # Plasma flow to the rest of the body [L/d]

  # Compartments
  compartment Lung := VlunC*BW                               # Lung
  compartment Skin := (FSkinExposed*BSA*SkinThickness)/1000  # Skin compartment
  compartment Ven_Plas := VvenC*VPlasC*BW                    # Venous plasma
  compartment Art_Plas := VartC*VPlasC*BW                    # Arterial plasma
  compartment Gut := VGC*BW                                  # Gut compartment
  compartment Liv := VLC*BW                                  # Liver compartment
  compartment Fat := VFC*BW                                  # Fat compartment
  compartment Kid := VKC*BW                                  # Kidney compartment
  compartment Fil := VfilC*BW                                # kidney filtrate compartment
  compartment Rest := (FBW-VLC-VFC-VKC-VfilC-VGC-VPlasC-VlunC)*BW - Skin  # Rest-of-body compartment
  compartment Delay                                          # Storage compartment of urine
  compartment Urine                                          # Urine compartment

  # Species
  substanceOnly species Alung in Lung          # amount in lung
  substanceOnly species ASk in Skin            # amount in skin
  substanceOnly species AVen_Plas in Ven_Plas  # amount in venous plasma
  substanceOnly species AArt_Plas in Art_Plas  # amount in arterial plasma
  substanceOnly species AGut in Gut            # amount in gut
  substanceOnly species ALiv in Liv            # amount in liver
  substanceOnly species AFat in Fat            # amount in fat
  substanceOnly species AKid in Kid            # amount in kidney
  substanceOnly species AFil in Fil            # amount in kidney filtrate
  substanceOnly species ADelay in Delay        # amount in storage compartment of urine
  substanceOnly species AUrine in Urine        # amount in urine
  substanceOnly species ARest in Rest          # amount in the rest of the body

  # Initial amounts
  Alung = 0
  ASk = 0
  AVen_Plas = 0
  AArt_Plas = 0
  AGut = 0
  ALiv = 0
  AFat = 0
  AKid = 0
  AFil = 0
  ADelay = 0
  AUrine = 0
  ARest = 0

  # Transfer equations
  Alung -> AArt_Plas; QCP*FreeLun*(Alung/Lung)
  AArt_Plas -> AGut; QG*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> ALiv; QL*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> AFat; QF*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> AKid; QK*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> AFil; Qfil*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> ARest; QR*Free*(AArt_Plas/Art_Plas)
  AArt_Plas -> ASk; QSk*Free*(AArt_Plas/Art_Plas)

  AGut -> ALiv; QG*FreeG*(AGut/Gut)
  AFil -> AKid; (Tm*(AFil/Fil))/(Kt+(AFil/Fil))
  AFil -> ADelay; Qfil*(AFil/Fil)
  ADelay -> AUrine; kurine*ADelay

  ALiv -> AVen_Plas; (QL+QG)*FreeL*(ALiv/Liv)
  AFat -> AVen_Plas; QF*FreeF*(AFat/Fat)
  AKid -> AVen_Plas; QK*FreeK*(AKid/Kid)
  ARest -> AVen_Plas; QR*FreeR*(ARest/Rest)
  ASk -> AVen_Plas; QSk*FreeSk*(ASk/Skin)

  AVen_Plas -> Alung; QCP*Free*(AVen_Plas/Ven_Plas)

end
