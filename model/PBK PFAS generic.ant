// Based on Westerhout et al., 2024 PFOA PBPK model
// Generic PFAS model with oral and dermal exposure routes

model generic_PFAS()
  
  // TIMING
  day := time + 1
  dayoftheweek := (time + 1) % 7
  month := ceiling(day/(365/12))-(((ceiling(day/365))-1)*12)
  year := ceiling(day/(365))
  age := time / 365 # used as a starting point, rather than birth [years]
  
  // EXPOSURE PARAMETERS
  exposure_duration = 60*365 # Duration of exposure (d)
  age_start_derm_exp = 14 # dermal and inhalation exposure starting age (e.g. occupational, or personal care products; exposure may start at a later age than 0)
  age_stop_derm_exp = 50 # dermal and inhalation exposure stopping age (e.g. occupational, or personal care products; exposure may stop at an earlier age than 80)
  
  // Oral exposure 
  Oraldose = 0.000187 # µg/kg/day TWI of 4.4 ng/kg/wk assuming PFOA:PFOS ratio of 24:76 from [EFSA opinion 2020, p.152]
  Drinkconc = 0 # Drinking water concentration (ug/L or ppb) 
  Drinkrate = 13 # Drinking water rate (mL/kg/day)
  
  // Dermal exposure 
  Dermconc = 10      # Dermal concentration (µg/L) ~median value for body lotion [Husoy 2023, table S3]
  Dermal_duration = 12 # Dermal exposure duration (e.g. skin lotion = 16 h, whereas shampoo is 0.25 h)
  Cdermal_day := Dermconc*Dermal_duration/24 # Correction of the daily dermal dose based on exposure duration
  fss = 0.005 # fraction of the total body surface exposed. 0.005 = area palm of hands (Sheridan et al., 1995, Rhodes et al., 2013)
  
  // PHYSIOLOGY
  BWbirth = 3.68 # Body weight at birth in kg new add opinion 2020 
  BW := BWbirth + 4.47*age - 0.093*age^2 + 0.00061*age^3
  SkinTarea := 9.1*((BW*1000)^0.666)  # Total area of the skin (cm^2)
  Skinarea := fss*SkinTarea # Total area of the exposed skin (cm^2)
  Skinthickness = 0.1 # Skin thickness (cm) 
  
  // Perfusion fractions
  QCC = 300;     # 12.5*24   # Cardiac blood output (L/d/kg^0.75)
  QFC = 0.052;   # Fraction cardiac output going to fat
  QLC = 0.069;   # Fraction cardiac output going to liver suggested by EFSA is 0.069, since they corrected for output to the gut Luccisano used 0.25, but Brown actually report 0.23
  QKC = 0.175;   # Fraction cardiac output going to kidney
  QSkC = 0.058;  # Fraction cardiac output going to skin
  QGC = 0.181;   # Fraction of cardiac output going to gut and the liver via portal arthery
  
  // Fraction tissue volumes
  VLC = 0.026;   # Fraction liver volume
  VFC = 0.214;   # Fraction fat volume
  VKC = 0.004;   # Fraction kidney volume
  VfilC = 0.0004;# Fraction filtrate compartment volume (10% of kidney volume)
  VGC = 0.0171;  # Fraction gut volume
  VlunC = 0.007 # Fraction lung volume (ICRP 89)
  VPlasC = 0.0428;  # Fraction plasma volume (58% of blood)
  Htc = 0.44;  # hematocrit
  
  // Skin parameters 
  hsurf = 0.01 # applied layer thickness in cm, value is assumed to be 0.1 mm
  Vsurf := fss*SkinTarea*hsurf/1000 # volume of dermal layer on exposed skin surface (L) 1 m^2 = 10,000 cm^2 * cm = cm^3 = ml / 1000 = L
  hsc = 0.0015 # Stratum corneum thickness (cm) # [Krüse 2007]
  Vsc := fss*SkinTarea*hsc/1000 # volume of exposed stratum corneum (L) cm^2 * cm = cm^3 = ml / 1000 = L
  hve = 0.0100 # Viable epidermis thickness (cm) # [Krüse 2007]
  Vve := fss*SkinTarea*hve/1000 # volume of exposed viable epidermis (L) cm^2 * cm = cm^3 = ml / 1000 = L
  hcell = 0.00005 # Blood vessel wall thickness (cm) # [Burton 1954] = 0.5 um capillary wall thickness
  ffatsc = 0.05 # Fraction of fat in stratum corneum [Polak 2012]
  ffatve = 0.02 # Fraction of fat in viable epidermis [Polak 2012]
  ffatepi = 0.02 # Fraction of fat in epidermis [Polak 2012]
  ffatbl = 0.007 # Fraction of fat in blood [Polak 2012]
  Kpcell = 0.93 # cm/h; Krüse model is normalized to 1 cm^2 # Permeability coefficient from arterial wall into the blood compartment [Krüse 2007]
  CLcell := 24*Kpcell*fss*SkinTarea/1000 # clearance from arterial wall into the blood compartment (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
    
  //Physicochemical properties - PFOA
  MW = 414.07
  logP = 4.81
  VP = 2.34 # 10^0.37 # Pa [Zhang 2021]; 1 mmHg = 133.322368 Pa; 0.15 mmHg (https://haz-map.com/Agents/6596) = 20 Pa
  
  Kscve := ((1-ffatve)+(ffatve*(10^logP)))/((1-ffatsc)+(ffatsc*(10^logP)))
  Kver := ((1-ffatbl)+(ffatbl*(10^logP)))/((1-ffatepi)+(ffatepi*(10^logP)))
  Kpsc = 0.000088 # [Franko 2011]
  Kpve = 0.000088 # [Franko 2011]
  CLsc := 24*Kpsc*fss*SkinTarea/1000 # stratum corneum clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  CLve := 24*Kpve*fss*SkinTarea/1000 # viable epidermis clearance (L/h) cm^2 * cm/h = cm^3/h = ml/h / 1000 = L/h
  
  # Pab := 1/(10^(6.96-(1.04*log10(VP)) - 0.533*logP - 0.00495*MW))

  Tmc = 144000 # 6000*24   # Maximum resorption rate 
  Tm := Tmc*BW^0.75 #transporter maximum 
  Kt = 55   # Resorption affinity# same as monkey
  kurinec = 0.0072 # 0.0003*24 # urinary elimination rate constant (/d/kg^-0.25)# estimated 
  kurine := kurinec*BW^(-0.25)
  Free = 0.02  # Free fraction of PFOA in plasma# same as monkey 
  
  // Partition coefficients from Harada, et al 2005 
  PL = 2.2   # Liver/plasma partition coefficient of PFOA
  PF = 0.04   # Fat/plasma partition coefficient of PFOA
  PK = 1.05   # Kidney/plasma partition coefficient of PFOA
  PSk = 0.1   # Skin/plasma partition coefficient of PFOA
  PR = 0.12   # Rest of the body/plasma partition coefficient of PFOA
  PG = 0.05   # Gut/plasma coefficient of PFOA  
  PLun = 1.27   # Lung/plasma partition coefficient of PFOA; [Fabrega 2014]

  // Free fraction of chemical in tissues 
  
  FreeL := Free/PL;  # liver
  FreeF := Free/PF;  # fat
  FreeK := Free/PK;  # kidney
  FreeSk := Free/PSk;  # skin
  FreeR := Free/PR;  # rest of the body
  FreeG := Free/PG;  # gut
  FreeLun := Free/PLun;  # lung
  
  // Mother-child parameters - PFOA
  Maternal_conc = 2.0 # maternal concentration ng/mL at delivery [EFSA opinion 2020, p.152]
  PT= 0.74 # placenta transfer for PFOA, new add opinion 2020 
  Ratio= 0.03 # milk concentration/maternal serum concentration for PFOA during breastfeeding, new add opinion 2020 
  DECLINE = 0.077 # decline of PFOA in milk was 7.7% per month, new add opinion 2020 
  
  // Oral exposure via the mother
  Milkconcentration := Maternal_conc*Ratio # initial Milk concentration at birth ng/mL 
  Milkconsumption = 0.8 # milkconsumption L per day 
  Intakemilka := Milkconcentration*Milkconsumption# initial intake via breastfeeding first month
  Intakemilkb := Intakemilka*(1-DECLINE) #intakeviabreastfeedingsecondmonth 
  Intakemilkc := Intakemilkb*(1-DECLINE) #intakeviabreastfeeding3month
  Intakemilkd := Intakemilkc*(1-DECLINE) #intakeviabreastfeeding4month 
  Intakemilke := Intakemilkd*(1-DECLINE) #intakeviabreastfeeding5month
  Intakemilkf := Intakemilke*(1-DECLINE) #intakeviabreastfeeding6month 
  Intakemilkg := Intakemilkf*(1-DECLINE) #intakeviabreastfeeding7month 
  Intakemilkh := Intakemilkg*(1-DECLINE) #intakeviabreastfeeding8month 
  Intakemilki := Intakemilkh*(1-DECLINE) #intakeviabreastfeeding9month 
  Intakemilkj := Intakemilki*(1-DECLINE) #intakeviabreastfeeding10month 
  Intakemilkk := Intakemilkj*(1-DECLINE) #intakeviabreastfeeding11month 
  Intakemilkl := Intakemilkk*(1-DECLINE) #intakeviabreastfeeding12month
  
  CONCbirth := Maternal_conc*PT # PFOA concentration at birth, new add in 2020 
  APlasbirth := CONCbirth*Free*VPlasC*BWbirth # initial amount in plasma at birth 
  Aartbirth := 0.39*APlasbirth # initial amount in arterial plasma at birth
  Avenbirth := 0.61*APlasbirth # initial amount in venous plasma at birth
  AGbirth := APlasbirth*PG # 0.99929853 # initial amount in gut at birth 
  ALbirth := APlasbirth*PL # 66.8360764 # initial amount in liver at birth 
  AFbirth := APlasbirth*PF # 10.000151 # initial amount in fat at birth 
  AKbirth := APlasbirth*PK # 5.88575746 # initial amount in kidney at birth 
  ALunbirth := APlasbirth*PLun # initial amount in the lung tissue at birth
  ASkbirth := APlasbirth*PSk # initial amount in the skin tissue at birth
  ARbirth := APlasbirth*PR # 74.9108792 # initial amount in rest of the body at birth 
  Afilbirth := APlasbirth*0.0000209606 # initial amount filtrate at birth 
  Adelaybirth := APlasbirth*42.27910714 # initial amount in storage at birth 
  Aurinebirth := APlasbirth*769.1347494 # initial amount in urine at birth 
  
  // Time dependent exposure
  Oraldose_on := piecewise(Intakemilka/BW, age < 0.083, Intakemilkb/BW, age >= 0.083 && age < 0.167, Intakemilkc/BW, age >= 0.167 && age < 0.250, Intakemilkd/BW, age >= 0.250 && age < 0.333, Intakemilke/BW, age >= 0.333 && age < 0.417, Intakemilkf/BW, age >= 0.417 && age < 0.500, Intakemilkg/BW,age >= 0.500 && age < 0.583,Intakemilkh/BW,age >= 0.583 && age < 0.667,Intakemilki/BW,age >= 0.667 && age < 0.750, Intakemilkj/BW,age >= 0.750 && age < 0.833,Intakemilkk/BW,age >= 0.833 && age < 0.917, Intakemilkl/BW, age >= 0.917 && age < 1, Oraldose)  # (ug/day)
  Oraldose_PFAS := piecewise(Oraldose_on*BW, day < exposure_duration, 0)
  Drinkdose_PFAS := piecewise((Drinkconc*Drinkrate/1000)*BW, day < exposure_duration, 0)
  Dermaldose_PFAS := piecewise(Cdermal_day*Vsurf, age >= age_start_derm_exp && age <= age_stop_derm_exp, 0)
  Csurf_PFAS := piecewise(Cdermal_day, age >= age_start_derm_exp && age <= age_stop_derm_exp, 0)

  // Scaled cardiac output and perfusion
  QC := QCC*BW^0.75;  # Cardiac output adjusted for BW (L/d)
  QCP := QC*(1-Htc) ; # Cardiac output adjusted for plasma flow (L/d)
  QL := QLC*QCP;  # Scaled plasma flow to liver (L/d)
  QF := QFC*QCP;  # Scaled plasma flow to fat (L/d)
  QK := QKC*QCP;  # Scaled plasma flow to kidney (L/d)
  Qfil := 0.2*QK      # Plasma flow to filtrate compartment (L/d)# 20% of QK 
  QG := QGC*QCP;  # Scaled plasma flow to gut (L/d)
  QSk := QSkC*QCP #*(Skinarea/SkinTarea); # scaled plasma flow to the skin
  QR := QCP-QL-QF-QK-QG-QSk; # Plasma flow to the rest of the body.
  Qbal := QCP - (QR+QL+QF+QK+QG+QSk)
  # Qp := ((((1400-190)*(age)^2.5)/((age)^2.5 + 50)) + 190)*24 # L/d [ICRP 89] manual curve fit of age dependency
 
  // Scaled tissue volumes
  VL := VLC*BW;  # Scaled liver volume (L)
  VF := VFC*BW;  # Scaled fat volume (L)
  VK := VKC*BW;  # Scaled kidney volume (L)
  Vfil := VfilC*BW;  # Scaled filtrate compartment volume
  VG := VGC*BW;  # Gut volume (L)
  VPlas := VPlasC*BW;  # Scaled plasma volume(L)
  Vart_plas := 0.39*VPlas # [Brown 1997]
  Vven_plas := 0.61*VPlas # [Brown 1997]
  VSk := (Skinarea*Skinthickness)/1000;  # Skin volume (L)
  Vlun := VlunC*BW # Functional residual capacity # https://en.wikipedia.org/wiki
  VR := 0.84*BW - VL - VF - VK - Vfil - VG - VPlas - VSk - Vlun # Rest of the body volume (L) 
  Vbal := (0.84*BW)-(VL+VF+VK+Vfil+VG+VPlas+VSk+Vlun) # Balance check--better be 0 
  
  // Define compartments
  compartment Lung := Vlun # Lung
  compartment Sc := Vsc # Stratum corneum
  compartment Trans = 0.0000001 # Transfer compartment
  compartment Ve := Vve # Viable epidermis
  compartment Skin := VSk;  # Skin compartment
  compartment Ven := Vven_plas
  compartment Art := Vart_plas 
  compartment Gut := VG;  # Gut compartment
  compartment Liv := VL;  # Liver compartment
  compartment Fat := VF;  # Fat compartment
  compartment Kid := VK;  # Kidney compartment
  compartment Fil := Vfil # kidney filtrate compartment
  compartment Delay;  # Storage compartment of urine
  compartment Urine;  # Urine compartment
  compartment Rest := VR # Rest compartment
    
  substanceOnly species Alung in Lung
  substanceOnly species Asc in Sc
  substanceOnly species Atrans in Trans
  substanceOnly species Ave in Ve
  substanceOnly species ASk in Skin
  substanceOnly species Aven in Ven
  substanceOnly species Aart in Art
  substanceOnly species AG in Gut
  substanceOnly species AL in Liv
  substanceOnly species AF in Fat
  substanceOnly species AK in Kid
  substanceOnly species Afil in Fil
  substanceOnly species Adelay in Delay
  substanceOnly species Aurine in Urine
  substanceOnly species AR in Rest

  CG := AG/Gut
  CVG := CG/PG
  CL := AL/Liv
  CVL := CL/PL
  CF := AF/Fat
  CVF := CF/PF
  CK := AK/Kid
  CVK := CK/PK
  Cfil := Afil/Fil
  CSk := ASk/Skin
  CVSk := CSk/PSk
  CR := AR/Rest
  CVR := CR/PR
  CLun := Alung/Lung
  CVLun := CLun/PLun
  Csc := Asc/Sc
  Ctrans := Atrans/Trans
  Cve := Ave/Ve
  CvenFree := Aven/Ven
  Cven := CvenFree/Free
  CartFree := Aart/Art
  Cart := CartFree/Free
  
  Alung = ALunbirth
  Asc = 0
  Atrans = 0
  Ave = 0
  ASk = ASkbirth
  Aven = Avenbirth
  Aart = Aartbirth
  AG = AGbirth
  AL = ALbirth
  AF = AFbirth
  AK = AKbirth
  Afil = Afilbirth
  Adelay = Adelaybirth
  Aurine = Aurinebirth
  AR = ARbirth
  
  // Transfer equations
  
  // Exposure
  -> AG; Oraldose_PFAS + Drinkdose_PFAS # Oral
  -> Asc; 2*CLsc*Csurf_PFAS # Dermal # Absorption from surface is assumed to not influence the surface concentration
  Asc -> ; 2*CLsc*Csc # represents transfer from stratum corneum to surface based on original model. As the surface concentration is assumed to be constant, there is no surface compartment
  Asc -> Atrans; 2*CLsc*Csc # 2*CLsc*(Asc / Vsc)
  Atrans -> Ave; 2*CLve*(Ctrans/Kscve) # 2*CLve*((Atrans / 0.0000001)/Kscve)
  Atrans -> Asc; 2*CLsc*Ctrans # 2*CLsc*(Atrans / 0.0000001)
  Ave -> Atrans; 2*CLve*Cve # 2*CLve*(Ave / Vve)
  Ave -> ASk; CLcell*(Cve/Kver) # CLcell*(Ave / Vve)/Kver
  
  Alung -> Aart; QCP*CVLun*Free # QCP*(Alung / (Valv*Pab_PFOA + Vlun))
  Aart -> AG; QG*Cart*Free # QG*(Aart/Vart_plas)
  Aart -> AL; QL*Cart*Free # QL*(Aart/Vart_plas)
  Aart -> AF; QF*Cart*Free # QF*(Aart/Vart_plas)
  Aart -> AK; QK*Cart*Free # QK*(Aart/Vart_plas)
  Aart -> Afil; Qfil*Cart*Free # Qfil*(Aart/Vart_plas)
  Aart -> AR; QR*Cart*Free # QR*(Aart/Vart_plas)
  Aart -> ASk; QSk*Cart*Free # QSk*(Aart/Vart_plas)
  
  AG -> AL; QG*CG*FreeG # QG*FreeG*(AG/VG)
  Afil -> AK; (Tm*Cfil)/(Kt+Cfil) # (Tm*(Afil/Vfil))/(Kt+(Afil/Vfil))
  Afil -> Adelay; Qfil*Cfil # Qfil*(Afil/Vfil)
  Adelay -> Aurine; kurine*Adelay 
  
  AL -> Aven; (QL+QG)*CL*FreeL # (QL+QG)*FreeL*(AL/VL)
  AF -> Aven; QF*CF*FreeF # QF*FreeF*(AF/VF)
  AK -> Aven; QK*CK*FreeK # QK*FreeK*(AK/VK)
  AR -> Aven; QR*CR*FreeR # QR*FreeR*(AR/VR)
  ASk -> Aven; QSk*CSk*FreeSk # QSk*FreeSk*(ASk/VSk)
  
  Aven -> Alung; QCP*Cven*Free # QCP*(Aven/Vven_plas)
  
end
